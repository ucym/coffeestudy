<!DOCTYPE html>
<html lang="ja">
<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">

    <title>CoffeeScript 勉強会</title>

    <link rel="stylesheet" href="static/css/bootstrap.min.css" >
    <link rel="stylesheet" href="static/css/style.css">
    <link rel="stylesheet" href="static/css/codemirror.css">
    <link rel="stylesheet" href="static/css/cm-monokai.css">

    <script src="static/js/jquery-2.1.1.min.js"></script>
    <script src="static/js/bootstrap.min.js"></script>

    <script src="static/js/codemirror.js"></script>
    <script src="static/js/codemirror/coffeescript/coffeescript.js"></script>
    <script src="static/js/codemirror/javascript/javascript.js"></script>

    <script src="static/js/coffee-script.js"></script>
    <script src="static/js/socket.io.js"></script>

    <script type="text/coffeescript">
        TRANSITION_END_EVENTS = "webkitTransitionEnd otransitionend oTransitionEnd msTransitionEnd transitionend"

        compileToShow = (inputEditor, exportEditor) ->
            # console.log inputEditor.getValue()
            try
                exportEditor.setValue CoffeeScript.compile(inputEditor.getValue())
            catch e
                exportEditor.setValue """
                コンパイルエラー :
                    #{e.message} @ Line #{e.location.first_line}
                """

        $ ->
            myRoomId = null
            socket = io.connect "http://localhost:8181"

            #
            # Initialize Editors
            #
            $input  = $ "#coffee-input"
            $export = $ "#coffee-export"

            inputEditor = CodeMirror.fromTextArea $input[0],
                mode         : "coffeescript"
                lineNumbers  : true
                indentUnit   : 4
            $input.data("cm", inputEditor)

            exportEditor = CodeMirror.fromTextArea $export[0],
                mode         : "javascript"
                lineNumbers  : true
                indentUnit   : 4
                readOnly     : true

            inputEditor.on "change", (editor) ->
                compileToShow editor, exportEditor
                return if editor.currentRoomId isnt myRoomId
                socket.emit "update", {code: editor.getValue()}

                return


            #
            # Setup socket.io connection
            #
            socket.once "joined", (data) ->
                console.log "Received 'joined'"

                rooms = for room in data.rooms
                    liClass = if room is myRoomId then "class='bg-info active'" else ""
                    "<li #{liClass}><a href='#' data-roomId='#{room}'>#{room}<span class='edit-indicator'></span></a>"

                $rooms = $ rooms.join("")
                $rooms.find(".edit-indicator").on TRANSITION_END_EVENTS, -> $(this).removeClass "edit"
                $("#editors ul").empty().append $rooms

                inputEditor.currentRoomId = myRoomId
                inputEditor.setValue data.code
                inputEditor.setOption "readOnly", false
                return

            socket.on "userJoined", (data) ->
                roomId = data.roomId

                return if $("#editors [data-roomId='#{roomId}']").length isnt 0

                $indicator = $ "<li><a href='#' data-roomId='#{roomId}'>#{roomId}<span class='edit-indicator'></span></a>"
                $indicator.find(".edit-indicator").on TRANSITION_END_EVENTS, -> $(this).removeClass "edit"
                $("#editors ul").append $indicator.slideDown(300)
                return

            socket.on "bodyResponse", (res) ->
                inputEditor.currentRoomId = res.roomId
                inputEditor.setValue res.code
                inputEditor.setOption "readOnly", (res.roomId isnt myRoomId)
                return

            socket.on "updateOther", (data) ->
                $("#editors [data-roomId='#{data.roomId}'] .edit-indicator").addClass "edit"

                if inputEditor.currentRoomId is data.roomId and data.roomId isnt myRoomId
                    socket.emit "requestBody", {roomId: data.roomId}
                return

            socket.on "teardown room", (data) ->
                $("#editors [data-roomId='#{data.roomId}']").parent("li:first").slideUp 400, -> $(this).remove()
                return

            #
            # Listen DOM events
            #
            # Editor button events
            $("#coffee-save").on "click", ->
                localStorage.setItem "code", inputEditor.getValue()

                compileToShow inputEditor, exportEditor

                $alert = $("#save-alert")
                $alert.fadeToggle(200)
                setTimeout ->
                    $alert.fadeToggle(200)
                , 2000

            # Execute current activated code
            $("#coffee-run").on "click", ->
                $editor = $(".editor-container .tab-pane.active textarea[uncompiled]")
                code = $editor.data("cm").getValue()

                try
                    CoffeeScript.eval code

                catch ex
                    $("#myModal").modal("show").find(".modal-body").html """
                    <h3>#{ex.name}</h3>
                    <p>#{ex.message}</p>

                    <h4>Stack trace</h4>
                    <ul>
                        #{ex.stack.split("\n").map((item) -> "<li style='white-space:pre'>#{item}</li>").join("")}
                    </ul>
                    """

            # Resize editor
            do ->
                editorContainer = $(".editor-container")[0]
                editors = [inputEditor, exportEditor]

                $(window).resize ->
                    for e in editors
                        e.setSize "auto", editorContainer.clientHeight
                .resize()

            # Change room
            $("#editors").on "click", "[data-roomId]", ->
                $this = $(this)

                $("#editors [data-roomId]").parent().removeClass("active")
                $this.parent().addClass("active")

                seeingRoomId = $this.attr("data-roomId")
                socket.emit "requestBody", {roomId: seeingRoomId}
                return false

            $(window).on "beforeunload", ->
                "他のページに移動しますか？"

            $(window).on "keydown", (e) ->
                if (e.ctrlKey || e.metaKey) and e.keyCode is 83 # Ctrl(Cmd)+S
                    $("#coffee-save").click()
                    return false

            $(window).on "scroll", ->
                document.body.scrollTop = 0

            #
            # Startup
            #
            # Restore last saved code.
            do ->
                savedCode = localStorage.getItem "code"
                inputEditor.setValue(savedCode) if savedCode?

                myRoomId = localStorage.getItem "roomId"

                if not myRoomId
                    while myRoomId = prompt("好きなユーザー名を入力してください")
                        continue if myRoomId is ""
                        break

                    localStorage.setItem "roomId", myRoomId

                socket.emit "join", {
                    roomId   : myRoomId
                    code     : savedCode
                }

                console.log "Emited 'join'", {roomId: myRoomId}

                $(".navbar-nav li:eq(0) a").click()

            return
    </script>
</head>

<body>
    <div id="app-root">
        <div class="navbar navbar-inverse">
            <div class="container">
                <div class="navbar-left">
                    <button id="coffee-run" class="btn btn-primary navbar-btn" accesskey="alt+r">
                        <span class="glyphicon glyphicon-play"></span> Run current code
                    </button>

                    <button id="coffee-save" class="btn btn-primary navbar-btn">
                        <span class="glyphicon glyphicon-save"></span> Save my code
                    </button>

                    <span id="save-alert"  class="label label-info" style="display:none;white-space:nowrap;">Code saved!</span>
                </div>
            </div>
        </div>

        <div id="content-root">
            <div id="editors">
                <ul class="nav nav-pills nav-stacked">
                    <li data-roomId="fuck"><a href="#">test</a></li>
                </ul>
            </div>

            <div class="tab-content editor-container">
                <div id="tabpage-private" class="tab-pane active">
                    <div class="container-fluid">
                        <div class="col-xs-6 pane pane-left">
                            <textarea id="coffee-input" class="form-control editor" rows="8" uncompiled></textarea>
                        </div>

                        <div class="col-xs-6 pane pane-right">
                            <textarea id="coffee-export" class="form-control editor" rows="8" readonly></textarea>
                        </div>
                    </div>
                </div>
            </div>
        </div><!-- #app-root end -->
    </div>

    <div class="modal" id="myModal" tabindex="-1">
        <div class="modal-dialog modal-lg">
            <div class="modal-content panel-danger">
                <div class="modal-header panel-heading">
                    <button type="button" class="close" data-dismiss="modal" aria-hidden="true">&times;</button>
                    <h4 class="modal-title">Error!!</h4>
                </div>
                <div class="modal-body"></div>
                <div class="modal-footer">
                    <button type="button" class="btn btn-default" data-dismiss="modal">Close</button>
                </div>
            </div><!-- /.modal-content -->
        </div><!-- /.modal-dialog -->
    </div>
</body>
</html>
